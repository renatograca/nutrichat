plugins {
    id 'java'
    id 'jacoco'
}

group = 'com.mequi'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly('org.projectlombok:lombok:1.18.30')
    annotationProcessor('org.projectlombok:lombok:1.18.30')
    annotationProcessor('org.projectlombok:lombok-mapstruct-binding:0.2.0')

    implementation('org.mapstruct:mapstruct:1.6.3')
    annotationProcessor('org.mapstruct:mapstruct-processor:1.6.3')

    implementation('com.fasterxml.jackson.core:jackson-databind:2.15.2')
    implementation('com.google.inject:guice:5.1.0')
    implementation('org.slf4j:slf4j-simple:2.0.16')
    implementation('io.javalin:javalin:6.4.0')
    implementation('com.zaxxer:HikariCP:5.0.1')
    implementation('org.postgresql:postgresql:42.6.0')
    implementation('org.mindrot:jbcrypt:0.4')
    implementation('com.auth0:java-jwt:4.4.0')

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')
    testImplementation('org.mockito:mockito-core:5.5.0')
    testImplementation('org.mockito:mockito-junit-jupiter:5.5.0')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.defaultComponentModel=default'
    ]
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    environment "jwt_secret", "secret"
    useJUnitPlatform()
    jacoco {
        excludes = [
                'com/mequi/config/**',
                'com/mequi/**/entity/*',
                'com/mequi/**/dto/*'
        ]
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    'com/mequi/config/**',
                    'com/mequi/**/entity/*',
                    'com/mequi/mapper/*',
                    'com/mequi/routes/*',
                    'com/mequi/**/dto/*',
                    'com/mequi/Main.class',
                    'com/mequi/exceptions/*'
            ])
        }))
    }
}